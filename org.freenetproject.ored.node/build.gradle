plugins {
    id 'org.beryx.jlink' version '2.25.0'
    id 'ored.java-conventions'
}

dependencies {
    implementation project(':org.freenetproject.ored.support')
    implementation project(':org.freenetproject.ored.crypt')
    implementation project(':org.freenetproject.ored.l10n')
    implementation project(':org.freenetproject.ored.client')
    implementation project(':org.freenetproject.ored.config')
    implementation project(':org.freenetproject.ored.store')
    implementation project(path: ':org.freenetproject.ext', configuration: 'shadow')

    implementation 'net.java.dev.jna:jna-jpms:5.11.0'
    implementation 'net.java.dev.jna:jna-platform-jpms:5.11.0'

    testImplementation testFixtures(project(":org.freenetproject.ored.support"))
}

application {
    mainModule = 'org.freenetproject.ored.node' // name defined in module-info.java
    mainClass = 'freenet.nodeapp.NodeStarter'
}

jlink {
    options = [
            '--ignore-signing-information',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages',
            '--strip-debug'
    ]

    mainClass = "freenet.nodeapp.NodeStarter"

    jarExclude 'freenet-ext', '/freenet/support/**'

    imageDir = file("$rootDir/build/jlink")
    imageZip = file("$rootDir/build/jlink.zip")

    launcher {
        name = 'ored'
        jvmArgs = [
                '-Dnetworkaddress.cache.ttl=0',
                '-Dnetworkaddress.cache.negative.ttl=0',
                '-Xss512k',
        ]
        args = []
    }

    jpackage {
        imageOutputDir = file("$rootDir/build/jpackage")
        installerOutputDir = file("$rootDir/build/jpackage")
        imageName = 'Oldenet'
        installerName = 'Oldenet'
        appVersion = '7.5.1493'
    }
}