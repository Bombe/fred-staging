plugins {
    id 'org.beryx.jlink' version '2.25.0'
    id 'ored.java-conventions'
    id "com.google.osdetector" version "1.7.0"
}

dependencies {
    implementation project(':org.freenetproject.ored.support')
    implementation project(':org.freenetproject.ored.crypt')
    implementation project(':org.freenetproject.ored.l10n')
    implementation project(':org.freenetproject.ored.client')
    implementation project(':org.freenetproject.ored.config')
    implementation project(':org.freenetproject.ored.store')
    implementation project(path: ':org.freenetproject.ext', configuration: 'shadow')

    implementation 'net.java.dev.jna:jna-jpms:5.11.0'
    implementation 'net.java.dev.jna:jna-platform-jpms:5.11.0'
    implementation('net.harawata:appdirs:1.2.1') {
        exclude group: 'net.java.dev.jna', module: 'jna-platform'
    }

    testImplementation testFixtures(project(":org.freenetproject.ored.support"))
}

application {
    mainModule = 'org.freenetproject.ored.node' // name defined in module-info.java
    mainClass = 'freenet.node.NodeStarter'
}

jlink {
    options = [
            '--ignore-signing-information',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages',
            '--strip-debug'
    ]

    mainClass = "freenet.node.NodeStarter"

    jarExclude 'freenet-ext', '/freenet/support/**'

    imageDir = file("$rootDir/build/jlink")
    imageZip = file("$rootDir/build/jlink.zip")

    jpackage {
        imageOutputDir = file("$rootDir/build/jpackage")
        installerOutputDir = file("$rootDir/build/jpackage")
        imageName = 'Oldenet'
        installerName = 'Oldenet'
        appVersion = '7.5.1493'
    }
}

tasks.register('copyWrapper') {
    def wrapperCmdPath = "${tasks.jlink.imageDir}/bin";
    def wrapperConfPath = "${tasks.jlink.imageDir}/conf";
    def wrapperLibPath = "${tasks.jlink.imageDir}/lib/wrapper";

    dependsOn 'jlink'
    inputs.files tasks.jlink.imageDirAsFile, "${rootDir}/lib/wrapper"
    outputs.dirs wrapperCmdPath, wrapperConfPath, wrapperLibPath

    doLast {
        // Copy wrapper command executable
        copy {
            from "${rootDir}/lib/wrapper/bin/wrapper-macosx-universal-64"
            into wrapperCmdPath
            rename 'wrapper-(.+)', 'wrapper'
        }

        // Copy wrapper.conf
        copy {
            from "${rootDir}/lib/wrapper/conf/wrapper.conf"
            into wrapperConfPath
        }

        // Copy lib
        copy {
            from "${rootDir}/lib/wrapper/lib/libwrapper-macosx-universal-64.jnilib"
            into wrapperLibPath
        }

        // Copy launch script
        copy {
            from "${rootDir}/lib/wrapper/bin/App.sh"
            into wrapperCmdPath
            rename 'App.sh', 'ored'
        }

        project.exec {
            commandLine('chmod', '+x', "${wrapperCmdPath}/wrapper")
            commandLine('chmod', '+x', "${wrapperCmdPath}/ored")
        }

        delete "${tasks.jlink.imageDir}/bin/org.freenetproject.ored.node", "${tasks.jlink.imageDir}/bin/org.freenetproject.ored.node.bat"
    }
}

tasks.named('clean').configure {
    delete += jlink.imageDir
    delete += jlink.imageZip
    delete += jlink.jpackageData.get().imageOutputDir
    delete += jlink.jpackageData.get().installerOutputDir
}